\section{Modelling REDFIN in Haskell}\label{sec-transformer}

In this section we formally define the REDFIN microarchitecture and express the
semantics of the instruction set as an explicit and symbolic state transformer.

\begin{figure}[t]
\begin{minted}[xleftmargin=10pt]{haskell}
data State = State
  { registers           :: RegisterBank
  , memory              :: Memory
  , instructionCounter  :: InstructionAddress
  , instructionRegister :: InstructionCode
  , program             :: Program
  , flags               :: Flags
  , clock               :: Clock }

type Register           = SymbolicValue Word2
type Value              = SymbolicValue Int64
type RegisterBank       = SymbolicArray Word2 Int64
type MemoryAddress      = SymbolicValue Word8
type Memory             = SymbolicArray Word8 Int64

type InstructionAddress = SymbolicValue Word8
type InstructionCode    = SymbolicValue Word16
type Program            = SymbolicArray Word8 Word16

data Flag               = Condition@\,@|@\,@Overflow@\,@|@\,@Halt@\,@...
type Flags              = SymbolicArray Flag Bool
type Clock              = SymbolicValue Word64
\end{minted}
\caption{Basic types for modelling REDFIN.\label{fig-types}}
\end{figure}

\subsection{The REDFIN Microarchitecture State}

The main idea of our approach is to use an explicit state transformer
semantics of the REDFIN microarchitecture. The \hs{State} of the entire
processing core is a product of states of every component, see
Fig.~\ref{fig-types}. We define \hs{SymbolicValue} and \hs{SymbolicArray} on top
of the SBV library~\cite{SBV} that we use as a frontend for SMT translation and
verification.

% \begin{equation*}
% \begin{split}
% S=\{(r, m, ic, ir, p, f, c) : r \in R, m \in M, ic \in A, \\ir \in I,
% p \in P, f \in F, c \in C\},
% \end{split}
% \end{equation*}

% \noindent
% where $R$ is the set of register bank configurations;
% $M$ is the memory state space;
% $A$~is the set of instruction addresses (the instruction counter $ic$ stores the
% address of the current instruction);
% $I$ is the set of instruction codes (the instruction register $ir$ stores the
% code of the current instruction);
% $P$ is the set of programs;
% $F$ is the set of the flag register configurations; and
% $C$ is the set of clock values.

% Fig.~\ref{fig-types} shows the translation of the above into Haskell types. Note
% that the types are not parameterised: recall that REDFIN is parameterised, e.g.
% the data width can be chosen depending on mission requirements, whereas we use fixed
% 64-bit data path for the sake of simplicity. The chosen names are self-explanatory,
% for example, the data type \hs{State} directly corresponds to the set of states~$S$.

% \todo{In principle, any other SMT frontend
% can be used, but to the best of our knowledge, SBV is the most mature SMT library
% available for Haskell. \textbf{No, SBV here is crucial because it does all the actual symbolic execution}} We briefly overview all \hs{State} components below.

% \subsubsection{Data values, registers and memory}

There are~4 registers (addressed by \hs{Word2}) and 256 memory cells (addressed
by \hs{Word8}) that store 64-bit values (\hs{Int64}). The register bank and
memory are represented by symbolic arrays
that can be accessed via SBV's functions \hs{readArray} and \hs{writeArray}.
REDFIN uses 16-bit \hs{InstructionCode}s, whose 6 leading bits contain the
opcode, and the remaining 10 bits hold instruction arguments. The \hs{Program}
maps 8-bit instruction addresses to instruction codes.

The microarchitecture status \hs{Flags} support conditional branching, track
integer overflow, and terminate the program (we omit a few other flags for
brevity).
The \hs{Clock} is a 64-bit counter
incremented on each clock cycle. Status flags and the clock are used for
diagnostics, formal verification, and worst-case execution time analysis.

\subsection{Instruction and Program Semantics}

We can now define the formal semantics of REDFIN instructions and programs as a
\emph{state transformer} $T : S \rightarrow S$, i.e. a function that maps
states to states. We distinguish instructions and programs by using
Haskell's list notation, e.g. $T_{\subhs{nop}}$ is the semantics of the
instruction $\hs{nop} \in I$, whereas $T_{\subhs{[}\subhs{nop}\subhs{]}}$ is the
semantics of the single-instruction program $\hs{[}\hs{nop}\hs{]} \in P$.
\footnote{REDFIN does not have a dedicated \hs{nop} instruction, but it can be
expressed as a jump to the next instruction, i.e.~\hs{jmpi 0}.}

\vspace{-1.5mm}
\noindent\hrulefill~\\
\vspace{-3.5mm}

\noindent
\textbf{Definition (program semantics):} The semantics of a program $p \in P$
is inductively defined as follows:

% \begin{itemize}
% \item
    The semantics of the \emph{empty program} $\hs{[}\hs{]} \in P$ coincides with
    the semantics of the instruction \hs{nop} and is the identity state transformer:
    $T_{\subhs{[}\subhs{]}} = T_{\subhs{nop}} = \hs{id}$.

    % \item
    The semantics of a \emph{single-instruction program} $\hs{[}\hs{i}\hs{]} \in P$
    is a composition of (i) fetching the instruction from
    the program memory~$T_\textit{fetch}$, (ii) incrementing the
    instruction counter~$T_\textit{inc}$, and (iii) the state transformer
    of the instruction itself~$T_{\subhs{i}}$; or,~using the order of state
    components from Fig.~\ref{fig-types}:
    \vspace{-1mm}
    \[
    \begin{array}{lcl}
    T_\textit{fetch} & = & (r, m, ic, ir, p, f, c) \mapsto (r, m, ic, p[ic], p, f, c + 1)\\
    T_\textit{inc} & = & (r, m, ic, ir, p, f, c) \mapsto (r, m, ic + 1, ir, p, f, c)\\
    T_{\subhs{[}\subhs{i}\subhs{]}} & = & T_{\subhs{i}} \circ T_\textit{inc} \circ T_\textit{fetch}\\
    \end{array}
    \]

    \noindent
    The semantics of a \emph{composite program} $\hs{i}\hs{:}\hs{p} \in P$,
    where the operator~\hs{:}~prepends an instruction $\hs{i} \in I$ to a program
    $\hs{p} \in P$, is defined as $T_{\subhs{i}\subhs{:}\subhs{p}} = T_{\hs{p}} \circ T_{\subhs{[}\subhs{i}\subhs{]}}$.

% \end{itemize}

\vspace{-1mm}
\noindent\hrulefill~\\
\vspace{-3mm}

% \noindent
We represent state transformers in Haskell using the \emph{state monad}, a
classic approach to emulating mutable state in a purely functional programming
language~\cite{wadler1990comprehending}. We call our state monad~\hs{Redfin} and
define it as follows\footnote{A generic version of this monad is available in
the standard module \hs{Control.Monad.State}.}:


\begin{minted}[xleftmargin=10pt]{haskell}
data Redfin a = Redfin { transform :: State -> (a, State) }
\end{minted}


\noindent
A computation of type~\hs{Redfin}~\hs{a} yields a value of type~\hs{a}~and
possibly alters the \hs{State} of the REDFIN microarchitecture.
The type \hs{Redfin}~\hs{()} describes a computation that does not produce any
value as part of the state transformation; such computations directly correspond
to state transformers.

\noindent
For example, here is the state transformer $T_\textit{inc}$:


\begin{minted}[xleftmargin=10pt]{haskell}
incrementInstructionCounter :: Redfin ()
incrementInstructionCounter = Redfin $ \current -> ((), next)
  where
    next = current { instructionCounter = instructionCounter current + 1 }
\end{minted}


\noindent
In words, the state transformer looks up the value of the \hs{instructionCounter}
in the \hs{current} state and replaces it in the \hs{next} state with the
incremented value. We can compose such primitive computations into more complex
state transformers using Haskell's \hs{do}-notation:


\begin{minted}[xleftmargin=10pt]{haskell}
readInstructionRegister :: Redfin InstructionCode
readInstructionRegister = Redfin $ \s -> (instructionRegister s, s)

executeInstruction :: Redfin ()
executeInstruction = do
    fetchInstruction
    incrementInstructionCounter
    instructionCode <- readInstructionRegister
    decodeAndExecute instructionCode
\end{minted}


\noindent
Here \hs{readInstructionRegister} reads the instruction code from the current
state \emph{without modifying it}, and is subsequently used in \hs{executeInstruction},
which defines the semantics of the REDFIN execution cycle. We omit definitions of
\hs{fetchInstruction} and \hs{decodeAndExecute} for brevity. The latter is a
case analysis of 47 opcodes that returns the matching instruction. We discuss
several instructions below.

\subsubsection{Halting the Processor}
The instruction~\hs{halt} sets the flag~\hs{Halt}, which stops the execution of
the current subroutine until a new one is started by a higher-level system
controller that resets~\hs{Halt}.


\begin{minted}[xleftmargin=10pt]{haskell}
halt :: Redfin ()
halt = writeFlag Halt true
\end{minted}


\noindent
The auxiliary function \hs{writeFlag} modifies the flag:


\begin{minted}[xleftmargin=10pt]{haskell}
writeFlag :: Flag -> SymbolicValue Bool -> Redfin ()
writeFlag flag value = Redfin $ \s -> ((), s')
  where
    s' = s { flags = writeArray (flags s) (flagId flag) value }
\end{minted}


\noindent
In the rest of the paper we will use auxiliary functions \hs{readRegister},
\hs{writeRegister}, \hs{readState}, etc.; they are simple state transformers
defined similarly to \hs{writeFlag}.

\subsubsection{Arithmetics}
The instruction \hs{abs} is more involved:
it reads a register and writes back the absolute value of its contents.
The semantics accounts for the potential integer overflow that leads to the
\emph{negative resulting value} when the input is $-2^{63}$ (REDFIN uses the
common two's complement signed number representation). The overflow is flagged
by setting~\hs{Overflow}. We use SBV's symbolic \emph{if-then-else}
operation~\hs{ite} to \emph{merge} two symbolic values~---~in this case two
possible next states, one of which is a state with the \hs{Overflow} flag set:


\begin{minted}[xleftmargin=10pt]{haskell}
abs :: Register -> Redfin ()
abs reg = do
    state  <- readState
    result <- fmap Prelude.abs (readRegister reg)
    let (_, state') = transform (writeFlag Overflow true) state
    writeState $ ite (result .< 0) state' state
    writeRegister reg result
\end{minted}


\subsubsection{Conditional Branching}
As an example of a control flow instruction consider \hs{jmpi_ct}, which
tests the~\hs{Condition} flag, and adds the provided \hs{offset} to the
instruction counter if the flag is set.


\begin{minted}[xleftmargin=10pt]{haskell}
jmpi_ct :: SymbolicValue Int8 -> Redfin ()
jmpi_ct offset = do
    ic <- readInstructionCounter
    condition <- readFlag Condition
    let ic' = ite condition (ic + offset) ic
    writeInstructionCounter ic'
\end{minted}


\noindent
% After working through the above examples, it is worth noting that
We use our Haskell encoding of the state transformer as a~\emph{metalanguage}:
we operate the REDFIN core as a puppet master, using external meta-notions of
addition, comparison and let-binding. From the processor's
point of view, we have infinite memory and act instantly, which gives us unlimited
modelling power. For example, we can simulate the processor environment
in an external tool and feed its result to \hs{writeRegister} as if it was
obtained in one clock cycle.

\subsection{Symbolic simulation}

Having defined the semantics of REDFIN programs, we can perform
\emph{symbolic processor simulation}.

There are many flavours of symbolic execution~\cite{SurveySymExec-CSUR18}
and there is no single answer to the question of which one is the best.
The choice of the symbolic execution technique depends heavily on the
verification scenarios. The verification framework for REDFIN is designed
to deal with two main classes of programs: (i) arithmetic calculations that
are statically provable to be terminating and (ii) control programs with
unbounded loops whose termination depend on dynamic input data. We approach
verification of the former using~\emph{symbolic execution with merging} that
allows for whole-program verification by translating the program and the
verification condition into a single SMT formula. However, unconditional merging
does not work for non-terminating programs.~To~verify those, we use an
alternative approach to symbolic execution, which constructs tree-shaped traces
with multiple execution paths that can be analysed and verified separately. The
presented framework thus works in two modes --- \emph{merging} and
\emph{branching} --- see examples in~\S\ref{sec-verification}
and~\S\ref{sec-motor-control}, respectively.

\subsubsection{Merging Mode}

The function \hs{simulate} takes a number of simulation steps~$N$ and an initial
symbolic \hs{State} as input, and runs \hs{executeInstruction} defined above~$N$
times. In each \hs{state} we merge two possible futures: (i) if the \hs{Halt}
flag is set, we stop the simulation and remain in the current \hs{state},
since in this case the processor must remain idle; (ii) otherwise we continue
the simulation from the~\hs{future}~state.


\begin{minted}[xleftmargin=10pt]{haskell}
simulate :: Int -> State -> State
simulate steps state =
    let halted = readArray (flags state) (flagId Halt)
    in if steps <= 0
       then state
       else let future = snd (transform executeInstruction state)
            in  ite halted state (simulate (steps-1) future)
\end{minted}


\noindent
The function~\hs{ite} performs symbolic merging of two possible next states,
depending on whether the program has \hs{halted}. As we have seen, the semantics
of individual instructions, as captured by \hs{executeInstruction}, also uses
\hs{ite} to merge possible next states when encountering choices, thereby
resulting in a linear simulation path.

\subsubsection{Branching Mode}

In the branching mode, on the contrary, we do not perform any merging at all and
generate a tree of simulation paths where every node contains a program state
with an associated~\emph{path condition} --- a symbolic Boolean expression
that encodes the choices taken along the path leading to the node.

To implement the branching semantics, we modify the~\hs{Redfin} monad by adding
support for non-determinism and tracking of path conditions. While we omit
the details, below we show how the types of the main simulation functions,
\hs{executeInstruction} and \hs{simulate}, need to change. The former becomes a
non-deterministic function that can return multiple next states:

\begin{minted}[fontsize=\small,xleftmargin=10pt]{haskell}
executeInstruction :: (State, SymbolicValue Bool) -> [(State, SymbolicValue Bool)]
\end{minted}

\noindent
For example, it will return two states in the case of the conditional jump
instruction~\hs{jmpi_ct} instead of merging them with \hs{ite}. The path
condition (of type~\hs{SymbolicValue}~\hs{Bool}) will be conjoined with the jump
condition for the first returned state, and with its negation for the second.

The type of~\hs{simulate} becomes more involved too. Since the semantics of
individual instructions now produces multiple next states, the symbolic
execution trace naturally grows into a tree:

\begin{minted}[fontsize=\small,xleftmargin=10pt]{haskell}
data Tree a = Node a [Tree a]
\end{minted}
\begin{minted}[fontsize=\small,xleftmargin=10pt]{haskell}
simulate :: Int -> (State, SymbolicValue Bool) -> Tree (State, SymbolicValue Bool)
\end{minted}

\noindent
The simulation starts form an initial state with a path condition representing
the precondition of the program, which can be instantiated with~\hs{true} if no
precondition is imposed. Proceeding further, more successor states can stem from
every state, until either the \hs{halt} instruction is encountered or the maximum
number of simulation steps is reached.

Symbolic simulation is very powerful. It allows us to formally verify properties
of REDFIN programs by fixing some parts of the state to constant values (e.g.,
the program), and then making assertions on the symbolic part of the resulting
state, as demonstrated in sections~\S\ref{sec-verification}
and~\S\ref{sec-motor-control}.
