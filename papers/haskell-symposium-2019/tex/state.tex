
In this section we formally define the REDFIN microarchitecture and express the
semantics of the instruction set as an explicit and symbolic state transformer.

\begin{figure}[t]
\begin{minted}[xleftmargin=10pt]{haskell}
data State = State
  { registers           :: RegisterBank
  , memory              :: Memory
  , instructionCounter  :: InstructionAddress
  , instructionRegister :: InstructionCode
  , program             :: Program
  , flags               :: Flags
  , clock               :: Clock }
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
type Register           = SymbolicValue Word2
type Value              = SymbolicValue Int64
type RegisterBank       = SymbolicArray Word2 Int64
type MemoryAddress      = SymbolicValue Word8
type Memory             = SymbolicArray Word8 Int64
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
type InstructionAddress = SymbolicValue Word8
type InstructionCode    = SymbolicValue Word16
type Program            = SymbolicArray Word8 Word16
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
data Flag               = Condition@\,@|@\,@Overflow@\,@|@\,@Halt@\,@...
type Flags              = SymbolicArray Flag Bool
type Clock              = SymbolicValue Word64
\end{minted}
\vspace{-3.5mm}
\caption{Basic types for modelling REDFIN.\label{fig-types}}
\vspace{-5mm}
\end{figure}

\vspace{-1mm}
\subsection{The REDFIN microarchitecture state}
\vspace{-0.5mm}

The main idea of our approach is to use an explicit state transformer
semantics of the REDFIN microarchitecture. The \hs{State} of the entire
processing core is a product of states of every component, see
Fig.~\ref{fig-types}.
% The names and types of the components are self-explanatory.
We define \hs{SymbolicValue} and \hs{SymbolicArray} on top of the SBV
library~\cite{SBV} that we use as the SMT translation and verification frontend.

% \begin{equation*}
% \begin{split}
% S=\{(r, m, ic, ir, p, f, c) : r \in R, m \in M, ic \in A, \\ir \in I,
% p \in P, f \in F, c \in C\},
% \end{split}
% \end{equation*}

% \noindent
% where $R$ is the set of register bank configurations;
% $M$ is the memory state space;
% $A$~is the set of instruction addresses (the instruction counter $ic$ stores the
% address of the current instruction);
% $I$ is the set of instruction codes (the instruction register $ir$ stores the
% code of the current instruction);
% $P$ is the set of programs;
% $F$ is the set of the flag register configurations; and
% $C$ is the set of clock values.

% Fig.~\ref{fig-types} shows the translation of the above into Haskell types. Note
% that the types are not parameterised: recall that REDFIN is parameterised, e.g.
% the data width can be chosen depending on mission requirements, whereas we use fixed
% 64-bit data path for the sake of simplicity. The chosen names are self-explanatory,
% for example, the data type \hs{State} directly corresponds to the set of states~$S$.

% \todo{In principle, any other SMT frontend
% can be used, but to the best of our knowledge, SBV is the most mature SMT library
% available for Haskell. \textbf{No, SBV here is crucial because it does all the actual symbolic execution}} We briefly overview all \hs{State} components below.

% \subsubsection{Data values, registers and memory}

There are~4 registers (addressed by \hs{Word2}) and 256 memory cells (addressed
by \hs{Word8}) that store 64-bit values (\hs{Int64}). The register bank and
memory are represented by symbolic arrays
that can be accessed via SBV's functions \hs{readArray} and \hs{writeArray}.
% \subsubsection{Instructions and programs}
REDFIN uses 16-bit \hs{InstructionCode}s, whose 6 leading bits contain the
opcode, and the remaining 10 bits hold instruction arguments. The \hs{Program}
maps 8-bit instruction addresses to instruction codes.

% \subsubsection{Status flags and clock}
The microarchitecture status \hs{Flags} support conditional branching, track
integer overflow, and terminate the program (we omit a few other flags for
brevity).
% The flag register is a symbolic map from flags to Boolean values.
The \hs{Clock} is a 64-bit counter
incremented on each clock cycle. Status flags and the clock are used for
diagnostic, formal verification and worst-case execution time analysis.

\subsection{Instruction and program semantics}

We can now define the formal semantics of REDFIN instructions and programs as a
\emph{state transformer} $T : S \rightarrow S$, i.e. a function that maps
states to states. We distinguish instructions and programs by using
Haskell's list notation, e.g. $T_{\subhs{nop}}$ is the semantics of the
instruction $\hs{nop} \in I$, whereas $T_{\subhs{[}\subhs{nop}\subhs{]}}$ is the
semantics of the single-instruction program $\hs{[}\hs{nop}\hs{]} \in P$.

% \footnote{REDFIN does not have a dedicated \hs{nop}
% instruction, but one can use the semantically equivalent instruction \hs{jmpi 0}
% instead (i.e. jump to the next instruction).}

\vspace{-3mm}
\noindent\hrulefill~\\
\vspace{-4.5mm}

\noindent
\textbf{Definition (program semantics):} The semantics of a program $p \in P$
is inductively defined as follows:

% \begin{itemize}
% \item
    The semantics of the \emph{empty program} $\hs{[}\hs{]} \in P$ coincides with
    the semantics of the instruction \hs{nop} and is the identity state transformer:
    $T_{\subhs{[}\subhs{]}} = T_{\subhs{nop}} = \hs{id}$.

    % \item
    The semantics of a \emph{single-instruction program} $\hs{[}\hs{i}\hs{]} \in P$
    is a composition of (i) fetching the instruction from
    the program memory~$T_\textit{fetch}$, (ii) incrementing the
    instruction counter~$T_\textit{inc}$, and (iii) the state transformer
    of the instruction itself~$T_{\subhs{i}}$, or, using the order of state
    components from Fig.~\ref{fig-types}:
    \vspace{-1mm}
    \[
    \begin{array}{lcl}
    T_\textit{fetch} & = & (r, m, ic, ir, p, f, c) \mapsto (r, m, ic, p[ic], p, f, c + 1)\\
    T_\textit{inc} & = & (r, m, ic, ir, p, f, c) \mapsto (r, m, ic + 1, ir, p, f, c)\\
    T_{\subhs{[}\subhs{i}\subhs{]}} & = & T_{\subhs{i}} \circ T_\textit{inc} \circ T_\textit{fetch}\\
    \end{array}
    \]

    % \item
    % \vspace{-1mm}
    The semantics of a \emph{composite program} $\hs{i}\hs{:}\hs{p} \in P$,
    where the operator~\hs{:}~prepends an instruction $\hs{i} \in I$ to a program
    $\hs{p} \in P$, is defined as $T_{\subhs{i}\subhs{:}\subhs{p}} = T_{\hs{p}} \circ T_{\subhs{[}\subhs{i}\subhs{]}}$.

% \end{itemize}

\vspace{-2mm}
\noindent\hrulefill~\\
\vspace{-3.5mm}

% \noindent
We represent state transformers in Haskell using the \emph{state monad}, a
classic approach to emulating mutable state in a purely functional programming
language~\cite{wadler1990comprehending}. We call our state monad~\hs{Redfin} and
define it as follows:
% \footnote{A generic version of this monad is available in standard module
% \hs{Control.Monad.State}.}

\begin{minted}[xleftmargin=10pt]{haskell}
data Redfin a = Redfin
  { transform :: State -> (a, State) }
\end{minted}

\noindent
Every computation with the return type~\hs{Redfin}~\hs{a} yields a value of type~\hs{a}
and possibly alters the \hs{State} of the REDFIN microarchitecture. As an example,
below we express the state transformer $T_\textit{inc}$ using the \hs{Redfin} monad.

\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
incrementInstructionCounter :: Redfin ()
incrementInstructionCounter =
    Redfin $ \current -> ((), next)
  where next = current { instructionCounter =
          instructionCounter current + 1 }
\end{minted}

\noindent
In words, the state transformer looks up the value of the \hs{instructionCounter}
in the \hs{current} state and replaces it in the \hs{next} state with the
incremented~value. Such computations directly correspond to REDFIN programs and
can be described using Haskell's powerful \hs{do}-notation:

% The type \hs{Redfin}~\hs{()} indicates that the computation does not
% produce any value as part of the state transformation.

% and can be composed using the operator \hs{>>}.
% For example, \hs{fetchInstruction}\\\hs{>>}~\hs{incrementInstructionCounter} is
% the state transformer $T_\textit{inc} \circ T_\textit{fetch}$ assuming that
% \hs{fetchInstruction} corresponds to $T_\textit{fetch}$. We can also use

\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
readInstructionRegister@\,@::@\,@Redfin@\,@InstructionCode
readInstructionRegister =
  Redfin $ \s -> (instructionRegister s, s)
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
executeInstruction :: Redfin ()
executeInstruction = do
  fetchInstruction
  incrementInstructionCounter
  instructionCode <- readInstructionRegister
  decodeAndExecute instructionCode
\end{minted}

\noindent
Here \hs{readInstructionRegister} reads the instruction code from the current
state \emph{without modifying it}. This function is used in \hs{executeInstruction},
which defines the semantics of the REDFIN execution cycle. We omit definitions of
\hs{fetchInstruction} and \hs{decodeAndExecute} for brevity. The latter is a
case analysis of 47 opcodes that returns the matching instruction. We discuss
several instructions below.

% \subsubsection{Halting the processor}
The instruction~\hs{halt} sets the flag~\hs{Halt}, thereby stopping the
execution of the current subroutine until a new one is started by a higher-level
system controller that resets \hs{Halt}.

\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
halt :: Redfin ()
halt = writeFlag Halt true
\end{minted}

\noindent
The auxiliary functions \hs{writeFlag}, \hs{readRegister} etc. are simple
state transformers manipulating parts of the \hs{State}.

% \begin{minted}{haskell}
% writeFlag :: Flag -> SymbolicValue Bool -> Redfin ()
% writeFlag flag value = Redfin $ \s -> ((), s')
%   where s' = s { flags = writeArray (flags s)
%                          (flagId flag) value }
% \end{minted}

% \subsubsection{Arithmetics}
The instruction \hs{abs} is more involved:
it reads a register and writes back the absolute value of its contents.
The semantics accounts for the potential integer overflow that leads to the
\emph{negative resulting value} when the input is $-2^{63}$ (REDFIN uses the
common two's complement signed number representation). The overflow is flagged
by setting~\hs{Overflow}.
% \footnote{We use \hs{Prelude.abs} to distinguish between the instruction
% and the function from the standard library \hs{Prelude}; \hs{fmap} applies
% \hs{Prelude.abs} to the result of \hs{readRegister}.}

\vspace{-0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
abs :: Register -> Redfin ()
abs rX = do
    state  <- readState
    result <- fmap Prelude.abs (readRegister rX)
    let (_, state') =
      transform (writeFlag Overflow true) state
    writeState $ ite (result .< 0) state' state
    writeRegister rX result
\end{minted}

\noindent
SBV's symbolic \emph{if-then-else} operation~\hs{ite} \emph{merges} two possible
next states, one of which has the \hs{Overflow} flag set.

% The
% auxiliary functions \hs{readRegister}, \hs{writeRegister}, \hs{readState} and
% \hs{writeState} are simple state transformers like
% \hs{readInstructionRegister} and \hs{writeFlag}.

% \subsubsection{Conditional branching}
As an example of a control flow REDFIN instruction consider \hs{jmpi_ct}, which
tests the~\hs{Condition} flag, and adds the provided offset to the instruction
counter if the flag is set.

\vspace{-0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
jmpi_ct :: SymbolicValue Int8 -> Redfin ()
jmpi_ct offset = do
  ic <- readInstructionCounter
  condition <- readFlag Condition
  let ic' = ite condition (ic + offset) ic
  writeInstructionCounter ic'
\end{minted}

\noindent
% After working through the above examples, it is worth noting that
We use our Haskell encoding of the state transformer as a~\emph{metalanguage}:
we operate the REDFIN core as a puppet master, using external meta-notions of
addition, comparison and let"/binding. From the processor's
point of view, we have infinite memory and act instantly, which gives us unlimited
modelling power. For example, we can simulate the processor environment
in an external tool and feed its result to \hs{writeRegister} as if it was
obtained in one clock cycle.

\vspace{-1mm}
\subsection{Symbolic simulation}
\vspace{-1mm}

Having defined the semantics of REDFIN programs, we can perform \emph{symbolic
processor simulation}. The function \hs{simulate} takes a number of simulation
steps~$N$ and an initial symbolic \hs{State} as input, and executes
\hs{executeInstruction} defined above $N$ times. In each \hs{state} we
merge two possible futures: (i) if the \hs{Halt} flag is set, we continue the
simulation from the \hs{next} state, (ii) otherwise we remain in the current
\hs{state}, since in this case the processor must remain idle.

\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
simulate :: Int -> State -> State
simulate steps s | steps <= 0 = s
                 | otherwise  =
  ite halted s (simulate (steps - 1) next)
 where halted = readArray (flags s) (flagId Halt)
       next   = snd (transform executeInstruction s)
\end{minted}

\noindent
%TODO: What about program synthesis?
Symbolic simulation is very powerful. It allows us to formally verify properties
of REDFIN programs by fixing some parts of the state to constant values (e.g.,
the program), and then making assertions on the resulting values of
the symbolic part of the state, as demonstrated in the next
section~\S\ref{sec-verification}.

